group 'org.krobot'
version '2.3.1'

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = 'krobot-framework'

compileJava.options.encoding = 'UTF-8'

repositories {
    jcenter()

    maven {
        url 'http://wytrem.github.io/maven/'
    }
}

dependencies {
    compile 'net.dv8tion:JDA:3.1.1_219'
    compile 'com.google.inject:guice:4.1.0'
    compile 'org.apache.logging.log4j:log4j-api:2.8.2'
    compile 'org.apache.logging.log4j:log4j-core:2.8.2'
    compile 'org.jetbrains:annotations:15.0'
    compile 'com.google.code.gson:gson:2.8.1'
}

jar {
    from 'LICENSE'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId group
            artifactId archivesBaseName
            version = version

            from components.java

            artifact sourcesJar {
                classifier 'sources'
            }

            artifact javadocJar
        }
    }

    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}
